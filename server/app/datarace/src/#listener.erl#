
%%====================================================================
%% listener
%%====================================================================

-module(listener).
-behaviour(gen_fsm).

-export([start_link/1]).
-export([init/1, connect/2, login/2, terminate/3, handle_info/3]).

-include("../include/types.hrl").

-type socket() :: none().


%%====================================================================
%% Server API
%%====================================================================


%% @doc Starts a new Listener process, waiting for incoming client
%% connections. Once a connection is established, it will wait for the
%% client to log in and spawn a new client_serv process to handle the
%% connection.
-spec start_link(ListenSocket) -> Result when
      Result :: {ok, pid()} | 
		ignore | 
		{error, Error},
      Error :: {already_started, pid()} |
	       term(),
      ListenSocket :: socket().

start_link(ListenSocket) ->
    gen_fsm:start_link(?MODULE, ListenSocket, []).


%%====================================================================
%% Callback functions
%%====================================================================


%% @doc Initializes the Listener and tells the gen_fsm behaviour to  
%% go to and execute the connect state directly.
-spec init(ListenSocket) -> {ok, connect, ListenSocket} when
      ListenSocket :: socket().

init(ListenSocket) ->
    process_flag(trap_exit, true),
    gen_fsm:send_event(self(), initialized),
    {ok, connect, ListenSocket}.


%% @doc Waits for incoming connections. Moves to the login state if 
%% a connection was successfully established. Stops the process 
%% otherwise.
-spec connect(initialized, ListenSocket) -> Result when
      ListenSocket :: socket(),
      AcceptSocket :: socket(),
      Reason :: term(),
      Result :: {next_state, login, AcceptSocket} | 
		{stop, Reason, ListenSocket}.

connect(initialized, ListenSocket) ->
    case gen_tcp:accept(ListenSocket) of
    	{ok, AcceptSocket} ->
    	    {ok, {Address, Port}} = inet:peername(AcceptSocket),
    	    log_serv:log("User connected with IP " ++ inet_parse:ntoa(Address) ++ 
    			     " on port " ++ integer_to_list(Port)),
    	    listener_sup:start_listener(),
    	    {next_state, login, AcceptSocket};
    	{error, Reason} ->
    	    listener_sup:start_listener(),
    	    {stop, Reason, ListenSocket}
    end.


%% @doc Handles login and registration instructions sent from a 
%% client. When a clients sends a valid user login packet, this 
%% callback will make the login, spawn a new client_serv process 
%% and transfer control over AcceptSocket to the spawned process, 
%% then terminate. When a user register packet is sent, the 
%% callback will try the registration and terminate.
-spec login(Event, AcceptSocket) -> Result when
      Type :: binary(),
      Event :: binary() | {Type, binary()},
      AcceptSocket :: socket(),
      Result :: {stop, normal, AcceptSocket} | {next_state, login, AcceptSocket}.

login({?LOGIN, Packet}, AcceptSocket) ->
    {UserName, Password} = packconv:convert_pack(?LOGIN, Packet),
    case account:login(UserName, Password) of
	{ok, UserId} ->
	    log_serv:log("Logged in UID " ++ integer_to_list(UserId)),
	    case client_serv_sup:start_client_serv() of
		{ok, Pid} -> 
		    case gen_tcp:controlling_process(AcceptSocket, Pid) of
			ok -> 
			    client_serv:verify_control_transfer(Pid, AcceptSocket, UserId);
			{error, _Reason} -> 
			    log_serv:log("Socket control transfer failed for UID " ++ 
					     integer_to_list(UserId)),
			    account:logout(UserId)
		    end;
		{error, _Reason} ->
		    gen_tcp:send(AcceptSocket, ?LOGIN_FALSE_LOGGED_IN),
		    log_serv:log("Login failed because already logged in"),
		    account:logout(UserId)
	    end;
	{error, no_user} -> 
	    gen_tcp:send(AcceptSocket, ?LOGIN_FALSE_USERNAME),
	    log_serv:log("Login failed because wrong username");
	{error, wrong_password} ->
	    gen_tcp:send(AcceptSocket, ?LOGIN_FALSE_PASSWORD),
	    log_serv:log("Login failed because wrong password");
	{error, already_loggedin} ->
	    gen_tcp:send(AcceptSocket, ?LOGIN_FALSE_LOGGED_IN),
	    log_serv:log("Login failed because already logged in")
    end,
    {stop, normal, AcceptSocket};
login({?REGISTER, Packet}, AcceptSocket) ->
    {UserName, Password, Email} = packconv:convert_pack(?REGISTER, Packet),
    log_serv:log("Register user: " ++ UserName),  
    case account:register(UserName, Password, Email) of
	ok ->
	    log_serv:log("User " ++ UserName ++ " registered"),
	    gen_tcp:send(AcceptSocket, ?REGISTER_TRUE);
	{error, user_already_exist} ->
	    log_serv:log("User " ++ UserName ++ " already registered"),
	    gen_tcp:send(AcceptSocket, ?REGISTER_FALSE)
    end,
    {stop, normal, AcceptSocket}.


%% @doc Receives {tcp, Socket, Package} messages as events and 
%% forwards them to the correct state. Returns {stop, normal, 
%% Socket} if the message is {tcp_closed, Reason} or {tcp_error, 
%% Socket, Reason}, which will terminate the process and execute 
%% terminate/3.
-spec handle_info(Event, State, Socket) -> Result when
      Packet :: binary(),
      Socket :: socket(),
      Reason :: term(),
      Event :: {tcp, Socket, Packet} | 
	       {tcp_closed, Socket} | 
	       {tcp_error, Socket, Reason},
      State :: atom(),
      Socket :: socket(),
      Result :: {stop, normal, Socket}.      

handle_info({tcp, _, <<Type:1/binary, Packet/binary>>}, State, Socket) ->
    inet:setopts(Socket, [{active, once}]),
    ?MODULE:State({Type, Packet}, Socket);
handle_info({tcp_closed, _}, _State, Socket) ->
    log_serv:log("User disconnected unexpectedly."),
    {stop, normal, Socket};
handle_info({tcp_error, _, _Reason}, _State, Socket) ->
    {ok, {Address, Port}} = inet:peername(Socket),
	    log_serv:log("Tcp error from IP " ++ inet_parse:ntoa(Address) ++ 
			     " on port " ++ integer_to_list(Port)),  
    {stop, normal, Socket}.


%% @doc Executed when Listener is about to terminate.
-spec terminate(Reason, State, Socket) -> undefined when
      Reason :: term(),
      State :: atom(),
      Socket :: socket().

terminate(_Reason, State, _Socket) ->
    log_serv:log("Terminating listener in state " ++ atom_to_list(State)).
    